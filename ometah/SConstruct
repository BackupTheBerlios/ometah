# -*- coding: iso-8859-1 -*-
################################################################################
#  $Id: SConstruct,v 1.14 2005/11/04 16:23:17 nojhan Exp $
#  Copyright : Free Software Fundation
#  Author : Johann Dréo <nojhan@gmail.com>
################################################################################

# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU Lesser General Public License
# as published by the Free Software Foundation; either version 2.1 of
# the License, or (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
# or FITNESS FOR A PARTICULAR PURPOSE. See the GNU Lesser General Public
# License for more details.
# 
# You should have received a copy of the GNU Lesser General
# Public License along with this program; if not, write to the Free

# Software Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA
# 02111-1307 USA


# flags for optimization, use them in production
foptim = '-fcaller-saves -fcse-follow-jumps -fcse-skip-blocks  \
          -felide-constructors -fexpensive-optimizations -ffast-math \
          -ffloat-store -funroll-all-loops -funroll-loops -O3'

# flags for checking the code
#-Wstrict-prototypes -Wmissing-prototypes 
fcheck = '-Wall -ansi -pedantic -Wimplicit \
          -Wredundant-decls -Wreturn-type -Wunused'
          
# flags for debugging
fdebug = '-ggdb'

# more flags
fsup = '-Wno-deprecated'

# base directories
dir_main = ['common',        \
            'communication', \
            'metaheuristic', \
            'problem',       \
            'interface']

# additionnal directories
dir_lib = ['problem/CEC05', \
           'problem/registration', \
           'metaheuristic/estimation', \
           'metaheuristic/random', \
           'metaheuristic/sampling', \
           'metaheuristic/neldermead', \
           'metaheuristic/genetic', \
           'metaheuristic/antcolony']
           #'interface/registration',


# setting the environnement
env = Environment(CCFLAGS = " ".join( [fcheck,fdebug,fsup] ), LIBPATH = dir_main+dir_lib )


# Configuration
conf = Configure(env)
have_cimg = False
if conf.CheckCXXHeader('CImg.h'):
    have_cimg = True
env = conf.Finish()


# other SConscript files
Export('env')
SConscript( [ l+'/SConscript' for l in dir_main ] )


# targets
# registration interface must have the CImg library available
if have_cimg == False:
    print "You must have the CImg.h library installed to build ometah_registration"
else:
    ometah_registration = env.Program( target = 'ometah_registration', \
        source = 'interface/registration/ometah_registration.cpp', \
        LIBS=['problem_registration','common','communication','metaheuristic','interface', \
            'sampling', 'estimation', 'neldermead', 'antcolony', 'genetic'] )

# standard interface
ometah = env.Program( target = 'ometah', source = 'interface/ometah.cpp', \
    LIBS=['common','communication','metaheuristic','problem','problem_CEC05','interface', \
          'estimation', 'random', 'sampling', 'neldermead','antcolony', 'genetic'] )


# simple example
ometah_example_simple = env.Program( target = 'example', source = 'interface/example_simple.cpp', \
    LIBS=['common','communication','metaheuristic','problem','problem_CEC05', \
          'estimation'] )


Default(ometah)
